// project plugins
plugins {
  id 'java'
  id 'jacoco'
  id 'com.diffplug.gradle.spotless' version '3.23.1'
  id 'org.openjfx.javafxplugin' version '0.0.7'
  id 'org.beryx.jlink' version '2.11.1'
}

// V-Sim version
version = '3.0.0'

// dependencies repositories
repositories {
  mavenCentral()
}

// compile and test dependencies
dependencies {
  compile 'commons-io:commons-io:2.6'
  compile 'com.jfoenix:jfoenix:9.0.8'
  compile 'commons-cli:commons-cli:1.4'
  compile files('lib/java-cup-11b-runtime.jar')
  compile 'org.fxmisc.richtext:richtextfx:0.10.1'
  testCompile 'org.junit.jupiter:junit-jupiter-api:5.4.2'
  testCompile 'org.junit.jupiter:junit-jupiter-params:5.4.2'
  testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

// javafx modules
javafx {
  version = '11.0.2'
  modules = ['javafx.base', 'javafx.controls', 'javafx.fxml', 'javafx.graphics']
}

// V-Sim main class
mainClassName = "vsim/vsim.VSim"

// jlink configuration
jlink {
  imageZip = project.file("${buildDir}/distributions/V-Sim-${version}-${javafx.platform.classifier}.zip")
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
  launcher {
    name = 'vsim'
    jvmArgs = [
      '--add-exports',
      'javafx.graphics/com.sun.javafx.application=vsim',
      '--add-exports',
      'javafx.controls/com.sun.javafx.scene.control=com.jfoenix',
      '--add-exports',
      'javafx.controls/com.sun.javafx.scene.control.behavior=com.jfoenix',
      '--add-exports',
      'javafx.graphics/com.sun.javafx.scene=com.jfoenix'
    ]
    windowsScriptTemplate = file('gradle/jlink/vsim.bat')
  }
  addExtraDependencies('javafx')
}

// test config
test {
  useJUnitPlatform()
  dependsOn 'cleanTest'
  testLogging {
    events 'passed', 'skipped', 'failed'
  }
}

// javac configuration
compileJava {
  options.compilerArgs += ['--add-exports', 'javafx.graphics/com.sun.javafx.application=vsim']
}

// jacoco configuration
jacocoTestReport {
  reports {
    xml.enabled true
  }
  afterEvaluate {
    classDirectories.from = files(classDirectories.files.collect {
      fileTree(
        dir: it,
        exclude: [
          // exclude gui package
          'vsim/gui/*',
          // exclude jlex and jcup classes
          'vsim/asm/Lexer.class',
          'vsim/asm/Parser.class',
          'vsim/asm/Parser$CUP$Parser$actions.class'
        ])
    })
  }
}

// spotless configuration
spotless {
  java {
    licenseHeaderFile 'gradle/spotless/HEADER'
    importOrder 'java', 'javafx', 'com', 'org', 'vsim', ''
    removeUnusedImports()
    trimTrailingWhitespace()
    indentWithSpaces(2)
    endWithNewline()
  }
}

// code coverage task
task codecov {
  dependsOn build, jacocoTestReport
  description "Calls build and then jacocoTestReport"
}

// java source and target compatibility
sourceCompatibility = 11
targetCompatibility = 11

// compile java source code in UTF-8
compileJava.options.encoding = 'UTF-8'
